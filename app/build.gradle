import groovy.swing.SwingBuilder

apply plugin: 'com.android.application'

android {
    signingConfigs {
        key {
            keyPassword 'dummy'
            keyAlias 'release signing key'
            storeFile file(
                    '../../Keystore/Keystore.jks')
            storePassword 'dummy'
        }
    }
    compileSdkVersion 24
    buildToolsVersion "24.0.1"
    defaultConfig {
        applicationId "paarmann.physikprofil"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 9
        versionName "1.8"
        signingConfig signingConfigs.key

        jackOptions {
          enabled true
        }
    }
    compileOptions {
      sourceCompatibility JavaVersion.VERSION_1_8
      targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }
    packagingOptions {
        exclude "META-INF/LICENSE.txt"
        exclude "META-INF/NOTICE.txt"
    }
    sourceSets { main { java.srcDirs = ['src/main/java'] }
                 test { java.srcDirs = ['src/test/java'] } }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'org.apache.commons:commons-io:1.3.2'
    compile 'org.apache.commons:commons-lang3:3.3.2'
    compile 'com.android.support:appcompat-v7:24.2.0'
    compile 'com.android.support:design:24.2.0'
    testCompile 'junit:junit:4.12'
    // compile files('libs/hwserver_cdk.jar')
    compile project(':homework_server_cdk')
}

// Ask for keystore & key password
gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.hasTask(':app:packageRelease')) {

        def pass = ''
        if (System.console() == null) {
            new SwingBuilder().edt {
                dialog(modal: true,
                       // Otherwise the build will continue running before you closed the dialog
                       title: 'Enter password', // Dialog title
                       alwaysOnTop: true, // pretty much what the name says
                       resizable: false, // Don't allow the user to resize the dialog
                       locationRelativeTo: null, // Place dialog in center of the screen
                       pack: true,
                       // We need to pack the dialog (so it will take the size of it's children)
                       show: true // Let's show it
                ) {
                    vbox { // Put everything below each other
                        label(text: "Please enter key passphrase:")
                        input = passwordField()
                        button(defaultButton: true, text: 'OK', actionPerformed: {
                            pass = input.password; // Set pass variable to value of input field
                            dispose(); // Close dialog
                        })
                    } // vbox end
                } // dialog end
            } // edt end
        } else {
            pass = System.console().readPassword("\nPlease enter key passphrase: ")
            pass = new String(pass)
        }

        if (pass.size() <= 0) {
            throw new InvalidUserDataException("You must enter a password to proceed.")
        }

        android.signingConfigs.key.keyPassword = pass
        android.signingConfigs.key.storePassword = pass

    } // end if has task
} // end whenReady
